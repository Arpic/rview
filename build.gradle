/*
 * Copyright (C) 2016 Jorge Ruesga
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    libraries = [
        support                     : '26.0.1',
        firebase                    : '11.2.2',
        google_api                  : '1.22.0',
        drive_api                   : 'v3-rev81-1.22.0',
        gson                        : '2.8.1',
        retrofit2                   : '2.3.0',
        okhttp3                     : '3.8.1',
        okio                        : '1.13.0',
        okhttp_digest               : '5028761430',
        rxjava                      : '2.1.3',
        rxandroid                   : '2.0.1',
        rxloader2                   : '1c07294ba2',
        commons_io                  : '2.5',
        antlrv3                     : '3.5.2',
        smoothprogressbar           : '1.1.0',
        picasso2                    : '2.5.2',
        picasso2_okhttp3_downloader : '1.1.0',
        prettytime                  : '4.0.1.Final',
        flexboxlayout               : '0.3.0',
        floatingsearchview          : '2d435bead5',
        androidsvg                  : '418cf67684',
        keyboardvisibilityevent     : '2.1.0',
        aboutlibraries              : '5.9.7',
        // https://s3.amazonaws.com/fabric-artifacts/public/com/crashlytics/sdk/android/crashlytics/maven-metadata.xml
        crashlytics                 : '2.6.8',
        // https://s3.amazonaws.com/fabric-artifacts/public/com/crashlytics/sdk/android/crashlytics-core/maven-metadata.xml
        crashlytics_core            : '2.3.17',
        // https://s3.amazonaws.com/fabric-artifacts/public/com/crashlytics/sdk/android/answers/maven-metadata.xml
        crashlytics_answers         : '1.3.13'
    ]

    testLibraries = [
        junit                       : '4.12',
        hamcrest                    : '1.4-atlassian-1',
        mockito                     : '2.9.0',
        powermock                   : '1.7.1',
        base64                      : '2.3.9'
    ]
}

allprojects {
    buildscript {
        repositories {
            google()
            jcenter()
            maven { url 'https://maven.fabric.io/public' }
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.3.3'
            classpath 'me.tatarka:gradle-retrolambda:3.7.0'
            classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.7.3'
            // https://s3.amazonaws.com/fabric-artifacts/public/io/fabric/tools/gradle/maven-metadata.xml
            classpath 'io.fabric.tools:gradle:1.24.1'
        }
    }

    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.fabric.io/public' }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

subprojects {
    afterEvaluate {project ->
        dependencies {
            testCompile "junit:junit:${testLibraries.junit}"
            testCompile "org.hamcrest:hamcrest-core:${testLibraries.hamcrest}"
            testCompile "org.hamcrest:hamcrest-library:${testLibraries.hamcrest}"
            testCompile "org.mockito:mockito-core:${testLibraries.mockito}"
            testCompile "org.powermock:powermock-module-junit4:${testLibraries.powermock}"
            testCompile "org.powermock:powermock-module-junit4-rule:${testLibraries.powermock}"
            testCompile "org.powermock:powermock-api-mockito2:${testLibraries.powermock}"
        }

        if (project.plugins.hasPlugin('android') || project.plugins.hasPlugin('android-library')) {
            android {
                compileSdkVersion 26
                buildToolsVersion '26.0.1'

                defaultConfig {
                    minSdkVersion 16
                    targetSdkVersion 26

                    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_1_8
                    targetCompatibility = JavaVersion.VERSION_1_8
                }

                packagingOptions {
                    exclude 'META-INF/DEPENDENCIES'
                    exclude 'META-INF/NOTICE'
                    exclude 'META-INF/LICENSE'
                    exclude 'META-INF/LICENSE.txt'
                    exclude 'META-INF/NOTICE.txt'
                    exclude 'META-INF/rxjava.properties'
                }

                testOptions {
                    unitTests.all {
                        testLogging {
                            events "passed", "skipped", "failed", "standardError"
                            outputs.upToDateWhen {false}
                        }
                    }
                }
            }
        }
    }
}