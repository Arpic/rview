/*
 * Copyright (C) 2016 Jorge Ruesga
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    libraries = [
        support                     : '25.1.1',
        firebase                    : '10.0.1',
        gson                        : '2.8.0',
        retrofit2                   : '2.1.0',
        retrofit2_rxjava2_adapter   : '1.0.0',
        okhttp3                     : '3.5.0',
        okio                        : '1.11.0',
        okhttp_digest               : 'ff0b9fb06f',
        rxjava                      : '2.0.5',
        rxandroid                   : '2.0.1',
        rxloader2                   : '3ee821c0e1',
        commons_io                  : '2.5',
        antlrv3                     : '3.5.2',
        smoothprogressbar           : '1.1.0',
        picasso2                    : '2.5.2',
        picasso2_okhttp3_downloader : '1.1.0',
        prettytime                  : '4.0.1.Final',
        flowlayout                  : '1.10',
        floatingsearchview          : 'f92e54c2da',
        androidsvg                  : '91e06d8a3a',
        aboutlibraries              : '5.9.1',
        // https://s3.amazonaws.com/fabric-artifacts/public/com/crashlytics/sdk/android/crashlytics/maven-metadata.xml
        crashlytics                 : '2.6.6',
        // https://s3.amazonaws.com/fabric-artifacts/public/com/crashlytics/sdk/android/crashlytics-core/maven-metadata.xml
        crashlytics_core            : '2.3.15',
        // https://s3.amazonaws.com/fabric-artifacts/public/com/crashlytics/sdk/android/answers/maven-metadata.xml
        crashlytics_answers         : '1.3.11'
    ]

    testLibraries = [
        junit                       : '4.12',
        hamcrest                    : '1.4-atlassian-1',
        mockito                     : '2.6.9'
    ]
}

allprojects {
    buildscript {
        repositories {
            jcenter()
            maven { url 'https://maven.fabric.io/public' }
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.2.3'
            classpath 'me.tatarka:gradle-retrolambda:3.5.0'
            classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.2'
            // https://s3.amazonaws.com/fabric-artifacts/public/io/fabric/tools/gradle/maven-metadata.xml
            classpath 'io.fabric.tools:gradle:1.22.1'
        }
    }

    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.fabric.io/public' }
    }
}

subprojects {
    afterEvaluate {project ->
        if (project.plugins.hasPlugin('android') || project.plugins.hasPlugin('android-library')) {
            android {
                compileSdkVersion 25
                buildToolsVersion '25.0.2'

                defaultConfig {
                    minSdkVersion 16
                    targetSdkVersion 25

                    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_1_8
                    targetCompatibility = JavaVersion.VERSION_1_8
                }

                packagingOptions {
                    exclude 'META-INF/DEPENDENCIES'
                    exclude 'META-INF/NOTICE'
                    exclude 'META-INF/LICENSE'
                    exclude 'META-INF/LICENSE.txt'
                    exclude 'META-INF/NOTICE.txt'
                    exclude 'META-INF/rxjava.properties'
                }
            }
        }
    }
}