/*
 * Copyright (C) 2016 Jorge Ruesga
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    libraries = [
        support                     : '25.0.0',
        gson                        : '2.8.0',
        retrofit2                   : '2.1.0',
        retrofit2_rxjava2_adapter   : '1.0.0',
        okhttp3                     : '3.4.1',
        okio                        : '1.11.0',
        okhttp_digest               : '2c51ebafcb',
        rxjava                      : '2.0.0',
        rxandroid                   : '2.0.0',
        rxloader2                   : '16d979b45c',
        commons_io                  : '1.3.2',
        antlrv3                     : '3.5.2',
        smoothprogressbar           : '1.1.0',
        picasso2                    : '2.5.2',
        picasso2_okhttp3_downloader : '1.1.0',
        prettytime                  : '4.0.1.Final',
        flowlayout                  : '1.10',
        floatingsearchview          : '0b237ad5bb',
        androidsvg                  : 'd4ec6d8d13',
        aboutlibraries              : '5.8.5',
        crashlytics                 : '2.6.5',
        crashlytics_core            : '2.3.14',
        crashlytics_answers         : '1.3.10'
    ]

    testLibraries = [
        junit                       : '4.12',
        hamcrest                    : '1.4-atlassian-1',
        mockito                     : '1.9.5'
    ]
}

allprojects {
    buildscript {
        repositories {
            jcenter()
            maven { url 'https://maven.fabric.io/public' }
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.2.2'
            classpath 'me.tatarka:gradle-retrolambda:3.3.1'
            classpath 'io.fabric.tools:gradle:1.22.0'
        }
    }

    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.fabric.io/public' }
    }
}

subprojects {
    afterEvaluate {project ->
        if (project.plugins.hasPlugin('android') || project.plugins.hasPlugin('android-library')) {
            android {
                compileSdkVersion 25
                buildToolsVersion '25.0.0'

                defaultConfig {
                    minSdkVersion 16
                    targetSdkVersion 25
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_1_8
                    targetCompatibility = JavaVersion.VERSION_1_8
                }

                packagingOptions {
                    exclude 'META-INF/DEPENDENCIES'
                    exclude 'META-INF/NOTICE'
                    exclude 'META-INF/LICENSE'
                    exclude 'META-INF/LICENSE.txt'
                    exclude 'META-INF/NOTICE.txt'
                    exclude 'META-INF/rxjava.properties'
                }
            }
        }
    }
}